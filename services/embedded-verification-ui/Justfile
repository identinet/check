#!/usr/bin/env -S just --justfile
# Documentation: https://just.systems/man/en/

set allow-duplicate-recipes := true

import '../verification-service-ui/Justfile'

# Print this help
default:
    @just -l

# Build release version of application
[group('development')]
build: install
    #!/usr/bin/env nu
    deno task build
    cd "{{ DIST_FOLDER }}"
    let buildManifest = open ".vite/manifest.json"
    cp -v $buildManifest."index.html".file ([($buildManifest."index.html".file | path dirname) "evi.js"] | path join)
    cp -v $buildManifest."index.html".css.0 ([($buildManifest."index.html".css.0 | path dirname) "evi.css"] | path join)

# Serve local assets
[group('development')]
serve: build
    static-web-server -p $PORT -d dist/assets -z -c '*'
