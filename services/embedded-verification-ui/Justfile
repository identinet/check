#!/usr/bin/env -S just --justfile
# Documentation: https://just.systems/man/en/

set allow-duplicate-recipes := true

import '../verification-service-ui/Justfile'

# Print this help
default:
    @just -l

# Build release version of application
[group('development')]
build: install
    #!/usr/bin/env nu
    deno task build
    cp -r config "{{ DIST_FOLDER }}/assets"
    cd "{{ DIST_FOLDER }}"
    let buildManifest = open ".vite/manifest.json"
    cp -v $buildManifest."index.html".file ([($buildManifest."index.html".file | path dirname) "evi.js"] | path join)
    cp -v $buildManifest."index.html".css.0 ([($buildManifest."index.html".css.0 | path dirname) "evi.css"] | path join)

# Build debug version of application
[group('development')]
dev-build: build
    #!/usr/bin/env nu
    watch src {|| just build }

# Serve local assets
[group('development')]
serve: build
    SERVER_CACHE_CONTROL_HEADERS=false SERVER_PORT=$PORT SERVER_DIRECTORY_LISTING=true SERVER_CORS_ALLOW_ORIGINS=* SERVER_ROOT=dist/assets static-web-server
